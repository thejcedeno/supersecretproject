// Import all gradle plugins
plugins {
    // Development
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle' version '2.4.3'
    id 'io.freefair.lombok' version '6.6.1'
    // Testing
    id 'jacoco'
}

// Project configuration
sourceCompatibility = 17
targetCompatibility = 17

group project.property("group")
archivesBaseName = project.property("pluginName")

ext {
    mcVersion = project.property("mcVersion")
}

// Repos and dependencies
repositories {
    // Use Maven Central for resolving dependencies.
    mavenLocal()
    mavenCentral()
    jitpack()
    maven { url = uri("https://repo.papermc.io/repository/maven-public/") }

}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:30.1.1-jre'
    // Mongo async/reactive streams 
    implementation 'org.mongodb:mongodb-driver-reactivestreams:4.8.0'
    // Paper using spigradle
    compileOnly paper(mcVersion)
    // Caffeine
    implementation("com.github.ben-manes.caffeine:caffeine:3.1.3")

    // Optional extensions
    implementation("com.github.ben-manes.caffeine:guava:3.1.3")
    implementation("com.github.ben-manes.caffeine:jcache:3.1.3")

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.144.3'
    testImplementation 'org.assertj:assertj-core:3.24.1'

    // Test containers for pogger things :B
    testImplementation "org.testcontainers:testcontainers:1.17.6"
    testImplementation "org.testcontainers:mongodb:1.17.6"

}

// Jar shadowing task.
shadowJar {
    classifier = ''
}

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    // depends = ['']
    excludeLibraries = ['*']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

tasks.build.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
